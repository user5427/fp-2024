<command> ::= <create_route> | <join_two_routes> | 
<join_two_routes_at_stop> | <join_two_trips> | 
<create_path> | <create_trip> |
<validate_trip> | <cleanup_trip> | <trip_distance>

<create_stop> ::= "create_stop(" <stop_id> ", " <name> ", " <coord_x> ", " <coord_y> ", " <next_stop_id> ", " <previous_stop_id> ")"
<create_route> ::= "create_route(" <route_id> ", " <route_name> ", " <list_of_stop_ids> ")"

<join_two_routes> ::= "join_two_routes(" <route> ", " <route> ", " <new_route_id> ", " <route_name> ")"
<join_two_routes_at_stop> ::= "join_two_routes_at_stop(" <route> ", " <route> ", " <stop_or_path_or_creat> ", " <new_route_id> ", " <route_name> ")"

<create_path> ::= "create_path(" <path_id> ", " <name> ", " <path_length> ", " <stop_id> ", " <stop_id> ")"

<create_trip> ::= "create_trip(" <trip_id> ", " <trip_name> ", " <list_of_stops_paths_creat> ")"
<join_two_trips> ::= "join_two_trips(" <trip> ", " <trip> ", " <new_trip_id> ", " <name> ")"

<cleanup_trip> ::= "cleanup_trip(" <trip> ", " <stops_or_paths> ", " <list_of_stops_and_paths> ")"
<validate_trip> ::= "validate_trip(" <trip> ", " <stops_or_paths> ", " <list_of_stops_and_paths>  ")"
<find_next_stop> ::= "find_next_stop(" <stop_or_path_or_creat> ", " <route> ")"
<trip_distance> ::= "trip_distance(" <trip> ", " <stop_or_path_or_creat> ", " <list_of_stops_and_paths> ")"

<list_of_stop_ids> ::= <stop_id> "," <list_of_stop_ids> | <stop_id>
<list_of_stops_paths_creat> ::= <stop_or_path_or_creat> "," <list_of_stops_paths_creat> 
                            | <stop_or_path_or_creat> 
<list_of_stops_and_paths> ::= <stops_or_paths> "," <list_of_stops_and_paths> 
                            | <stops_or_paths> 

<trip> ::= <trip_id> | <create_trip>
<route> ::= <route_id> | <create_route> | <join_two_routes> | <join_two_routes_at_stop>
<stop_or_path_or_creat> ::= <create_stop> | <stops_or_paths>
<stops_or_paths> ::= <stop_id> | <path_id>
<next_stop_id> ::= <integer>
<previous_stop_id> ::= <integer>
<stop_id> ::= <integer>
<name> ::= <string>
<coord_x> ::= <float>
<coord_y> ::= <float>
<route_id> ::= <integer>
<route_name> ::= <string>
<path_id> ::= <integer>
<path_length> ::= <float>
<trip_id> ::= <integer>
<new_trip_id> ::= <integer>
<new_route_id> ::= <integer>
<trip_name> ::= <string>
<float> ::= <pos> | <neg>
<integer> ::= [0-9]+
<pos> ::= ("0" |  [1-9] [0-9]*) ("." [0-9]+ )?
<neg> ::= "-" <pos>
<string> ::= <character> <string> | <character>
<character> ::= [1-9] | [A-Z] | [a-z] | "_"
